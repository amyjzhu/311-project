(* queries *)
query = edge-query | node-query

edge-query = "SELECT" attr "(" attr "," attr ")"
             ("FROM" ( "(" edge-query ")" | edge-query)?
             ("TRAVERSE" "BY" ("DEPTH" | "BREADTH"))?
             ("WHERE" expr)?
             ("LIMIT" expr)?

node-query = "SELECT" attr
             ("FROM" ( "(" node-query ")" | node-query)?
             ("TRAVERSE" "BY" ("DEPTH" | "BREADTH"))?
             ("WHERE" expr)?
             ("LIMIT" expr)?

(* attr will just be a valid indentifier in DAGQL. *)
attr = ID

(* expressions *)
expr = or-expr
or-expr = and-expr ("OR" and-expr)*
and-expr = not-expr ("AND" not-expr)*
not-expr = "NOT" not-expr | comparison-expr
comparison-expr = concat-expr (("=" | "<>" | ">" | "<" | ">=" | "<=") concat-expr)*
concat-expr = add-expr ("||" add-expr)*
add-expr = mul-expr (("+" | "-") mul-expr)*
mul-expr = unary-expr (("*" | "/") unary-expr)*
unary-expr = ("+" | "-") unary-expr | attr-expr
attr-expr = attr | attr "." attr | literal
literal = BOOL | STR | NUM

(* terminals *)
ID = [_A-Za-z][_A-Za-z0-9]*
BOOL = "FALSE" | "TRUE"
STR  = "'" [^']* "'"
NUM  = [0-9]+

(* Precedence Table for operators (tightest to loosest)
   .
   + - (unary)
   * /
   + - (binary) 
   ||
   = <> < > <= >=
   NOT
   AND
   OR
*)
